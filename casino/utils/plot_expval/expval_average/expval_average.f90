!---------------------------------------------------------------------------!
! EXPVAL AVERAGER                                                           !
! ===============                                                           !
! NDD, 2.2007                                                               !
!                                                                           !
! This utility allows the user to average over sets of data produced when   !
! plotting expectation values.  For example, if one were to calculate the   !
! PCF of a 2D HEG bilayer, one would have 4 particle types and therefore 10 !
! PCF components.  However the parallel-spin in-plane PCFs should be the    !
! same, the antiparallel in-plane PCFs should be the same and the opposite- !
! plane PCFs should be the same.  So we should average over the data sets   !
! within each of these groupings.                                           !
! Simply run expval_averager in a directory containing the lineplot.dat     !
! file generated by plot_expval and answer the questions.                   !
!                                                                           !
! Changes                                                                   !
! =======                                                                   !
! 03/08 NDD  Allow weighted averages, e.g. to construct total PCF.          !
! 06/09 NDD  Fixed bug in weighted averages.                                !
!---------------------------------------------------------------------------!

MODULE utils
 IMPLICIT NONE
 INTEGER,PARAMETER :: dp=kind(1.d0)


CONTAINS


 CHARACTER(12) FUNCTION i2s(n)
!-----------------------------------------------------------------------!
! Convert integers to left justified strings that can be printed in the !
! middle of a sentence without introducing large amounts of white space.!
!-----------------------------------------------------------------------!
  IMPLICIT NONE
  INTEGER,INTENT(in) :: n
  INTEGER i,j
  INTEGER,PARAMETER :: ichar0=ichar('0')
  i2s=''
  i=abs(n)
  do j=len(i2s),1,-1
   i2s(j:j)=achar(ichar0+mod(i,10))
   i=i/10 ; if(i==0)exit
  enddo ! j
  if(n<0)then
   i2s='-'//adjustl(i2s)
  else
   i2s=adjustl(i2s)
  endif ! n<0
 END FUNCTION i2s


END MODULE utils


PROGRAM expval_averager
!---------------------------!
! Main program starts here. !
!---------------------------!
 USE utils
 IMPLICIT NONE
 INTEGER nbins,nsets,ierr,i,j,ngroups,gp,commapos,sig1,gp2
 INTEGER,ALLOCATABLE :: set_in_group(:,:),nsets_in_group(:)
 REAL(dp) xt,rec_tot_wt
 REAL(dp),ALLOCATABLE :: x(:),y(:,:),wt(:,:)
 REAL(dp),PARAMETER :: tol=1.d-8
 CHARACTER(1) char1
 CHARACTER(80) char80

 write(6,*)
 write(6,*)'EXPVAL_AVERAGER'
 write(6,*)'==============='
 write(6,*)

 open(unit=8,file='lineplot.dat',status='old',iostat=ierr)
 if(ierr/=0)then
  write(6,*)'Error opening lineplot.dat'
  stop
 endif ! ierr/=0

! Count the number of sets
 nsets=1
 do
  read(8,*,iostat=ierr)char1
  if(ierr/=0)exit
  if(char1=='&')nsets=nsets+1
 enddo
 rewind(8)
 write(6,*)'There are '//trim(i2s(nsets))//' sets of data in lineplot.dat.'

! Count the number of bins (lines of data for each set).
 nbins=0
 do
  read(8,*,iostat=ierr)char1
  if(ierr/=0)exit
  if(char1=='&')exit
  nbins=nbins+1
 enddo
 rewind(8)
 write(6,*)'There are '//trim(i2s(nbins))//' lines of data in each set in &
  &lineplot.dat.'
 write(6,*)

 allocate(x(nbins),y(nbins,nsets),stat=ierr)
 if(ierr/=0)then
  write(6,*)'Allocation error.'
  stop
 endif ! ierr/=0

! Read the data.
 do j=1,nsets
  do i=1,nbins
   read(8,*,iostat=ierr)xt,y(i,j)
   if(ierr/=0)then
    write(6,*)'Error reading lineplot.dat.'
    stop
   endif ! ierr/=0
   if(j==1)then
    x(i)=xt
   else
    if(abs(x(i)-xt)>tol*xt)then
     write(6,*)'Error: x coordinates in lineplot.dat don''t agree.'
     stop
    endif ! x wrong
   endif ! j=1
  enddo ! i
  if(j<nsets)then
   read(8,*,iostat=ierr)char1    ! Read "&".
   if(ierr/=0)then
    write(6,*)'Error reading lineplot.dat.'
    stop
   endif ! ierr/=0
   if(char1/='&')then
    write(6,*)'Error reading lineplot.dat.'
    stop
   endif ! ierr/=0
  endif ! j<nsets
 enddo ! j
 close(8)

! Ask user to enter number of groups.
 do
  write(6,*)'Please enter the number of groups of sets to create.'
  read(5,*,iostat=ierr)ngroups
  if(ierr/=0)ngroups=-1
  if(ngroups>0.and.ngroups<=nsets)exit
  write(6,*)'This should be an integer between 1 and '//trim(i2s(nsets))//'.'
 enddo

 allocate(set_in_group(nsets,ngroups),nsets_in_group(ngroups), &
  &wt(nsets,ngroups),stat=ierr)
 if(ierr/=0)then
  write(6,*)'Allocation error.'
  stop
 endif ! ierr/=0

! Ask user to enter the sets in each group.
 do gp=1,ngroups
  getgroup: do
   write(6,*)'Please list the sets in group '//trim(i2s(gp)) &
    &//', separated by commas.'
   read(5,'(a)')char80
   do j=1,nsets
    commapos=index(char80,',')
    if(commapos==0)then
     read(char80,*,iostat=ierr)set_in_group(j,gp)
    else
     read(char80(1:commapos-1),*,iostat=ierr)set_in_group(j,gp)
    endif ! commapos=0
    if(ierr/=0)then
     write(6,*)'Please try again.'
     cycle getgroup
    endif ! ierr/0
    if(set_in_group(j,gp)<1.or.set_in_group(j,gp)>nsets)then
     write(6,*)'Set indices should be between 1 and '//trim(i2s(nsets))//'.'
     cycle getgroup
    endif ! bad group
    if(any(set_in_group(1:j-1,gp)==set_in_group(j,gp)))then
     write(6,*)'Set '//trim(i2s(set_in_group(j,gp))) &
      &//' appears twice in this group.'
     cycle getgroup
    endif ! set duplicated
    do gp2=1,gp-1
     if(any(set_in_group(1:nsets_in_group(gp2),gp2)==set_in_group(j,gp)))then
      write(6,*)'Set '//trim(i2s(set_in_group(j,gp)))//' appeared in group ' &
       &//trim(i2s(gp2))//'.'
      cycle getgroup
     endif ! set duplicated.
    enddo ! gp2
    if(commapos==0)then
     nsets_in_group(gp)=j
     if(gp==ngroups.and.sum(nsets_in_group(1:gp))/=nsets)then
      write(6,*)'Some sets have not been placed in a group.'
      cycle getgroup
     endif ! Sets left over
     exit getgroup
    else
     char80=char80(commapos+1:)
    endif ! commapos
   enddo ! j
   write(6,*)'Too many sets have been supplied.  Try again.'
  enddo getgroup
  do
   write(6,*)'Please enter weights for each set in the group, or enter a &
    &letter to set'
   write(6,*)'all weights to unity.'
   read(5,*,iostat=ierr)wt(1:nsets_in_group(gp),gp)
   if(ierr/=0)then
    write(6,*)'Will set weights to unity.'
    wt(1:nsets_in_group(gp),gp)=1.d0
   endif ! Error - assume user doesn't want to weight his or her data.
   if(any(wt(1:nsets_in_group(gp),gp)<0.d0))then
    write(6,*)'Weights cannot be negative.'
   elseif(sum(wt(1:nsets_in_group(gp),gp))<=0.d0)then
    write(6,*)'Sum of weigts must be greater than zero.'
   else
    exit
   endif ! Problem with weights.
  enddo ! Get weights
 enddo ! gp
 write(6,*)

! Average over data in each group.  Replace the first set in each group by
! the averaged data.
 do gp=1,ngroups
  sig1=set_in_group(1,gp)
  y(1:nbins,sig1)=y(1:nbins,sig1)*wt(1,gp)
  do j=2,nsets_in_group(gp)
   y(1:nbins,sig1)=y(1:nbins,sig1)+y(1:nbins,set_in_group(j,gp))*wt(j,gp)
  enddo ! j
  rec_tot_wt=1.d0/sum(wt(1:nsets_in_group(gp),gp))
  y(1:nbins,sig1)=y(1:nbins,sig1)*rec_tot_wt
 enddo ! gp

! Write out the averaged data to lineplot_averaged.dat.
 open(unit=9,file='lineplot_averaged.dat',status='replace',iostat=ierr)
 if(ierr/=0)then
  write(6,*)'Error opening lineplot_averaged.dat.'
  stop
 endif ! ierr/=0
 do gp=1,ngroups
  sig1=set_in_group(1,gp)
  do i=1,nbins
   write(9,*)x(i),y(i,sig1)
  enddo ! i
  if(gp<ngroups)write(9,*)'&'
 enddo ! gp
 close(9)
 write(6,*)'Averaged data has been written to lineplot_averaged.dat.'
 write(6,*)

 deallocate(x,y,set_in_group,nsets_in_group,wt)

 write(6,*)'Program finished.'
 write(6,*)

END PROGRAM expval_averager
